/* REXX ----------------------------------------------------------
   LIBRARY  : MDB.REXX.EXEC(WEBRET)
   VERSION  : 1.0
   CREATED  : 15 May 1997 4:30pm S.Cox
   NOTES    : WEBRET is called from the MDB_WEBRET_IBMOE.REXX rexx
            : exec in OE and runs under a full TSO/E environment.
	
   $Log:
    2    Met_DB_Project 1.1         21/01/2010 10:05:35    Sheila Needham
         Change SYS1.SDBLOAD to MDB.SYSLOAD
    1    Met_DB_Project 1.0         28/02/2006 10:12:14    Sheila Needham  
   $
   Revision 1.7  2000/04/07  10:33:29  10:33:29  usmdb (Generic MDB account)
   Added retrieval by minute. New variables - S.Cox

   Revision 1.6  99/06/10  11:38:37  11:38:37  usmdb (Generic MDB account)
   21-06-1999 - S.Cox
   Addition of subtype ATAFS - S.Cox

   Revision 1.5  99/01/11  16:49:07  16:49:07  usmdb (Generic MDB account)
   Increase size of output dataset

   Revision 1.4  98/08/10  12:59:15  12:59:15  usmdb (Generic MDB account)
   no entry

   Revision 1.3  1998/08/10 10:44:13  usmdb
   Addition of arguments sondeparts, area_n, area_s, area_w
   and area_e - S.Cox

   Revision 1.2  1998/07/31 13:32:44  usmdb
   no entry

   Revision 1.1  1998/07/29 13:15:49  usmdb
   Initial revision

   $Source: /home/us0400/mdb/op/lib/web_source/RCS/MDB_WEBRET_IBM.REXX,v $
------------------------------------------------------------------ */

parse upper arg subtype stns sdate shour sminute edate ehour eminute ,
                submit maxobs header versions increment sondeparts ,
                area_n area_s area_w area_e   /* 1.7 */

/* -----------------------------------------------------------------
   Translate '+' to ' ' in strings.
   ----------------------------------------------------------------- */

stns       = translate(stns,' ','+')
submit     = translate(submit,' ','+')
subtype    = translate(subtype,' ','+')
versions   = translate(versions,' ','+')
header     = translate(header,' ','+')
sondeparts = translate(sondeparts,' ','+')
area_n     = translate(area_n,' ','+')
area_s     = translate(area_s,' ','+')
area_w     = translate(area_w,' ','+')
area_e     = translate(area_e,' ','+')

say stns                           /* sysout.1 on omvs */

SELECT
WHEN submit = 'SUBMIT METDB RETRIEVAL' THEN
  DO
    say sdate'/'shour||sminute||'Z'   /* sysout.2 on omvs */   /* 1.7 */
    say edate'/'ehour||eminute||'Z'   /* sysout.3 on omvs */   /* 1.7 */
  END
OTHERWISE
  DO
    say '<BR>'
    say '<BR>'
  END
END

say submit                         /* sysout.4 on omvs */

dat = date(o)
tim = time(l)

lq123 = 'PUBLIC.MDB.WEBRET'
lq4   = '.D'||substr(dat,1,2)||substr(dat,4,2)||substr(dat,7,2)
lq5   = '.T'||substr(tim,1,2)||substr(tim,4,2)||substr(tim,7,2)
lq6   = '.M'||substr(tim,10,6)

dsnam = lq123||lq4||lq5||lq6

say dsnam                          /* sysout.5 on omvs */

"ALLOC DA('"dsnam"') FI(FT06F001) NEW LRECL(133)
 BLK(1330) RECFM(F B) SPACE(1 50) TRACKS RELEASE"      /* 1.5 */

"CALL 'MDB.SYSLOAD(WEBRET)'"

"FREE FI(FT06F001)"

say obtot                          /* sysout.6 on omvs */

EXIT 0
