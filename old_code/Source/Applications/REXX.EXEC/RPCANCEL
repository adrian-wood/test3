/* REXX ******************************************************* REXX */
/*                                                                   */
   jobname = MDBOE        /* testing */
   say 'jobname is' jobname

/*
  Allocate file for ejes scrollable output
                                                           */
   there =listdsi("'mcc3.DBtemp.out'")
   if there ^= 0 then
     do
       "alloc fi(ejesext) da('mcc3.dbtemp.out') storclas(scdatprk)
        mgmtclas(mcsnc1) recfm(f b) lrecl(505) blksize(27775) track
        space(10) new catalog"
     end
   else
     do
       "alloc fi(ejesext) da('mcc3.dbtemp.out') shr"
     end
/*
   Extract all active jobs matching the jobname pattern.
   Build the ejes command stack
                                                                     */
   newstack
   queue "AC"
   queue "extract first last"
   queue "nop"
   queue "return"
/*
   execute the ejes stack
        outtrap puts ejes output to the stem variable msg         */
   x = outtrap('msg.')
   'ejes yj,ys,yt,ya,j='jobname',batch'
   y = outtrap('off')
/*
   delete the stack and load the results of the extract command
                                                                     */
   delstack
   "free fi(ejesext)"
   "alloc fi(temp1) da('mcc3.dbtemp.out') shr"
   "execio * diskr temp1 (stem l. finis)"
   "free fi(temp1)"
/*  "delete 'mcc3.sntemp.out'"   */
   if l.0 <= 3 then
     do
       say 'No jobs matching the pattern:'jobname
       exit 8
     end
   say l.0-3' active 'jobname' jobs at 'DATE('S')
/*
   loop over lines selected, compiling a list of JobIDs to be
                                                     cancelled       */
/* find positions of keywords
                                   */
   ecol = pos('Elapsed',l.2)
   jcol = pos('JobName',l.2)
   icol = pos('JobID',l.2)
   scol = pos('StepName',l.2)
   if ecol = 0 | jcol = 0 | icol = 0 then
     do
       say 'Error in EJES extraction 'l.2
       exit 8
     end

   count = 0        /* number of jobs to be cancelled */
   do loop = 4 to l.0
      t.loop=substr(l.loop,ecol,11,) /* elapsed time */
      j.loop=substr(l.loop,jcol,6,) /* jobname      */
      i.loop=substr(l.loop,icol,8,) /* job id   */
      s.loop=substr(l.loop,scol,8,) /* stepname */
      if substr(j.loop,6,1) = ' ' | substr(s.loop,1,5) = 'STEP1' then
        do
          say j.loop i.loop t.loop 'MDBOE operational broker'
        end
      else      /* jobs MDBOEn n=1 to 9  */
        do
          say j.loop i.loop t.loop s.loop

/* elapsed time takes the form dd-hh:mm:ss for more than 24 hours */

          if substr(t.loop,3,1) = '-' then
            do
              count=count+1
              say ' over one day elapsed ' j.loop i.loop t.loop
              jx.count=j.loop        /* save info on jobs to cancel */
              ix.count=i.loop        /* jobname, jobid and elapsed  */
              tx.count=t.loop        /*                             */
            end
          else
            nop
        end
   end
/*
   loop over jobs to be cancelled
                                                                     */
   do loop= 1 to count
     say 'cancelling ' jx.loop ix.loop tx.loop' at 'time()
/* build a new command stack  */
     newstack
     queue "AC"
     queue "find "ix.loop
     queue ":k"
     queue "nop"
     queue "return"
/*
   execute the ejes stack
                                                                     */
     x = outtrap('msg.')
     'ejes yj,ys,yt,ya,j='jobname',batch'
     y = outtrap('off')
   end       /* back for next job */
/*
   exit 0
                    */

