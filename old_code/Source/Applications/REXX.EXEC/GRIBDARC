/* REXX --------------------------------------------------------------
 *
 * program     : GRIBARC
 *
 * description : To archive MetDB GRIB datasets into MASS.
 *
 * subtypes    : SEAICE, TCRTEMP, MFSST, NSST100, NSST50,             2
 *             : OSSEAICE, DRWIND, HRSEAICE, SNOW                     4
 *
 * author      : Simon Cox 27/05/02
 *
 * $Workfile: GRIBDARC$ $Folder: REXX.EXEC$
 * $Revision: 1$ $Date: 21/07/2010 11:06:39$
 *
 * $Log:
 *  1    Met_DB_Project 1.0         21/07/2010 11:06:39    Richard Weedon  Test
 *        version used for Radar Doppler 5 min bulletins
 * $
 * Revision 1.3  2006/02/06 12:04:35  usmdb
 * 1.3.  20 February 2006.  Brian Barwell.  CHG021439, CHG021779.
 * Additional statements to archive OSSEAICE data after 3 days.
 *
 * Revision 1.2  2003/02/20 11:25:58  usmdb
 * 2 new data types NSST100 and NSST50.
 * Brian Barwell.
 *
 * Revision 1.1  2002/07/03 10:52:27  usmdb
 * Initial revision
 *
 *--------------------------------------------------------------------*/

/*--------------------------------------------------------------------*
 * Initialisations.
 *--------------------------------------------------------------------*/

today_base = date('B')  /* todays date in BASE format (days since and
                           including the base date, 1 January 0001) */

totds   = 0   /* total number of datasets to archive */

details = 'DSCRDT2'  /* input to csilcat. Dataset creation date */

/*--------------------------------------------------------------------*
 * To add a new datatype to archive, increment datype.0 and add a
 * new datype.x and a new daysold.x (relates to datype.x)
 *--------------------------------------------------------------------*/

datype.0 = 1                     /* number of datatypes             4 */
datype.1 = 'MDB.DARADOP.**'     /* TCRTMP   dataset pattern */

daysold.1 = 5    /* archive dataset if created > 5 days ago */

say 'Start of GRIBARC. Current date = 'today_base
say
say 'Datasets to archive listed below:'

/*--------------------------------------------------------------------*
 * LIST GRIB DATASETS.
 *
 * Call csilcat to return dataset names and creation dates. Creation
 * date is in the form yyddd where ddd is the Julian day.
 *
 * Convert this date to BASE format (days since and including the base
 * date, 1 January 0001)
 *
 * Compare the creation date with the current date and add dataset to
 * array arcfile, if created > daysold.x ago.
 *--------------------------------------------------------------------*/

do i = 1 to datype.0   /* loop over datatypes */
  ds = datype.i
  parse value csilcat(ds,details,'A') with rc ',' dsn.0 ',' datasets
  do j = 1 to dsn.0   /* loop over datasets returned */
    parse var datasets dsn ';' dscrdt2 ',' datasets                          s
    crday = substr(dscrdt2,1,5)  /* dataset creation date yyddd */
    ds_base=date(B,crday,'J')  /* convert to base date */
    if (today_base-ds_base > daysold.i) then do
      totds = totds + 1
      arcfile.totds = dsn  /* add dataset name to arcfile array */
    end
  end
end

/*--------------------------------------------------------------------*
 * If no datasets to archive, issue error and exit.
 *--------------------------------------------------------------------*/

if (totds = 0) then do
  say 'GRIBARC: ERROR: No datasets to archive'
  exit 1
end

/*--------------------------------------------------------------------*
 * Loop over datasets. write to archive file.
 * commented out for test purposes
 *--------------------------------------------------------------------*/

/* "newstack"    /* new stack */

do i = 1 to totds
  say arcfile.i
  queue arcfile.i   /* add to stack */
end

"alloc fi(fileout) da('TDB.GRIBDARC.ARCHIVE') shr"
"execio * diskw fileout (FINIS"
"free fi(fileout)" */

/*--------------------------------------------------------------------*
 * Loop over datasets. write to scratch file.
 *--------------------------------------------------------------------*/

"newstack"    /* new stack */

do i = 1 to totds
  queue "   DELETE '"arcfile.i"'"        /* add to stack       ST5 */
/*  queue 'SELECT CATDSN='arcfile.i     previous version       ST5 */
end

"alloc fi(fileout) da('TDB.GRIBDARC.SCRATCH') shr"
"execio * diskw fileout (FINIS"
"free fi(fileout)" 

/*--------------------------------------------------------------------*
 * Submit job to archive the data
 *--------------------------------------------------------------------*/

"submit 'MDB.JCL.CNTL(GRIBDARC)'"

exit 0
