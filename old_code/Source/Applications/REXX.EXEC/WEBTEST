/* REXX ----------------------------------------------------------
   LIBRARY  : MDB.REXX.EXEC(WEBTEST)
   VERSION  : 1.0
   CREATED  : 15 May 1997 4:30pm S.Cox
   NOTES    : WEBTEST is called from the MDB_WEBTEST_IBMOE.REXX exec in OE
            : and runs under a full TSO/E environment
	
   $Log:
    2    Met_DB_Project 1.1         10/02/2010 12:19:01    Sheila Needham
         Changed SCLOAD to MDB.SYSLOAD
    1    Met_DB_Project 1.0         28/02/2006 10:12:14    Sheila Needham  
   $
   Revision 1.3  2000/04/07  10:34:39  10:34:39  usmdb (Generic MDB account)
   Added retrieval by minute. New variables - S.Cox

   Revision 1.2  99/06/10  11:39:03  11:39:03  usmdb (Generic MDB account)
   21-06-1999 - S.Cox
   Addition of subtype ATAFS - S.Cox

   Revision 1.1  98/07/29  13:16:54  13:16:54  usmdb (Generic MDB account)
   Initial revision

   $Source: /home/us0400/mdb/op/lib/web_source/RCS/MDB_WEBTEST_IBM.REXX,v $
------------------------------------------------------------------ */

parse upper arg subtype stns sdate shour sminute edate ehour eminute ,
                submit maxobs header versions increment sondeparts ,
                area_n area_s area_w area_e  /* 1.3 */

/* -----------------------------------------------------------------
   Translate '+' to ' ' in stns and submit strings.
   ----------------------------------------------------------------- */

stns       = translate(stns,' ','+')
submit     = translate(submit,' ','+')
subtype    = translate(subtype,' ','+')
versions   = translate(versions,' ','+')
header     = translate(header,' ','+')
sondeparts = translate(sondeparts,' ','+')
area_n     = translate(area_n,' ','+')
area_s     = translate(area_s,' ','+')
area_w     = translate(area_w,' ','+')
area_e     = translate(area_e,' ','+')

say stns                           /* sysout.1 on omvs */

SELECT
WHEN submit = 'SUBMIT METDB RETRIEVAL' THEN
  DO
    say sdate'/'shour||sminute||'Z'   /* sysout.2 on omvs */   /* 1.3 */
    say edate'/'ehour||eminute||'Z'   /* sysout.3 on omvs */   /* 1.3 */
  END
OTHERWISE
  DO
    say '<BR>'
    say '<BR>'
  END
END

say submit                         /* sysout.4 on omvs */

/* -----------------------------------------------------------------
   The LNDSYN program calls the MDB data extraction routine MDB()
   and simply extracts the latest SYNOP for the station specified.
   Variable "stn" is picked up in the program LNDSYN via the REXXVAR
   subroutine (available on MET.PROGLIB)
   ----------------------------------------------------------------- */

dat = date(o)
tim = time(l)

lq123 = 'PUBLIC.MDB.WEBRET'
lq4   = '.D'||substr(dat,1,2)||substr(dat,4,2)||substr(dat,7,2)
lq5   = '.T'||substr(tim,1,2)||substr(tim,4,2)||substr(tim,7,2)
lq6   = '.M'||substr(tim,10,6)

dsnam = lq123||lq4||lq5||lq6

say dsnam                          /* sysout.5 on omvs */

"ALLOC DA('"dsnam"') FI(FT06F001) NEW LRECL(133)
 BLK(1330) RECFM(F B) SPACE(1 50) TRACKS RELEASE"

"CALL 'MDB.SYSLOAD(WEBTEST)'"

"FREE FI(FT06F001)"

say obtot                          /* sysout.6 on omvs */

EXIT 0
