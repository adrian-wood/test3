SUBROUTINE SATOB1(NFT,POINT,BULLEND,BULL,TTAAII,CCCC,OCOR)

!-----------------------------------------------------------------------

! PROGRAM       : SATOB1
!
! PURPOSE       : DECODES SATOB SECTION ONE AND DETERMINES SECTION
!                 NUMBER OF NEXT SECTION
!
! CALLED BY     : STBBUL
!
! CALLS         : SATOB2, SATOB3, SATOB4, SATOB5, SATOB7,
!                 DATIM, STBIND, STBENC, AIRSTO
!
! ARGUMENTS     : (1) NFT      FT NUMBER (10 FOR SATOBS)
!                 (2) POINT    POINTER TO WHERE TO START IN BULLETIN
!                 (3) BULLEND  NUMBER OF LAST CHARACTER IN BULLETIN
!                 (4) BULL     REPORT DATA
!                 (5) TTAAII   BULLETIN IDENTIFIER
!                 (6) CCCC     COLLECTING CENTRE
!                 (7) OCOR     CORRECTION FLAG
!
! REVISION INFO :
!
!
! $Workfile: satob1.f90$ $Folder: OpSource$
! $Revision: 2$ $Date: 20/01/2011 16:51:16$
!
! CHANGE RECORD :
!
! $Log:
!  2    MetDB_Refresh 1.1         20/01/2011 16:51:16    Rosemary Lavery ref to
!        module IVALUE added
!  1    MetDB_Refresh 1.0         12/01/2011 14:12:35    Rosemary Lavery
!       Initial Port
! $
!
!-----------------------------------------------------------------------
! (C) CROWN COPYRIGHT 2011 - MET OFFICE. All Rights Reserved.
!
! Met Office, United Kingdom.
!
! The use, duplication and disclosure of this code is strictly
! prohibited without the permission of The Meteorological Database
! Team at the above address.
!-----------------------------------------------------------------------

USE AIRSTO_mod
USE DATIM_mod
USE IDES_mod
USE IVALUE_mod
USE METDB_COM_mod, ONLY: MISSIN
USE SATOB2_mod
USE SATOB3_mod
USE SATOB4_mod
USE SATOB5_mod
USE SATOB7_mod
USE STBIND_mod
USE STBENC_mod

IMPLICIT NONE

! Interface Arguments

INTEGER, INTENT(IN)               :: NFT
INTEGER, INTENT(INOUT)            :: POINT
INTEGER, INTENT(IN)               :: BULLEND
CHARACTER (LEN=*), INTENT(INOUT)  :: BULL
CHARACTER (LEN=*), INTENT(IN)     :: TTAAII
CHARACTER (LEN=*), INTENT(IN)     :: CCCC
LOGICAL, INTENT(IN)               :: OCOR

! Local Variables

INTEGER  :: BLKSIZ
INTEGER  :: DATETIME(5)
INTEGER  :: DAY
INTEGER  :: DECADE
INTEGER  :: DESCR(1000)
INTEGER  :: ENDDCV
INTEGER  :: HOUR
INTEGER  :: I
INTEGER  :: J
INTEGER  :: JUMP
INTEGER  :: K
INTEGER  :: L
INTEGER  :: M
INTEGER  :: MESSLEN
INTEGER  :: MINUTE
INTEGER  :: MONTH
INTEGER  :: MONTHDAY(12)
INTEGER  :: NELEM
INTEGER  :: NN
INTEGER  :: NOBS
INTEGER  :: NOW(8)          ! Current date/time from system clock
INTEGER  :: NOWYEAR
INTEGER  :: NYEAR
INTEGER  :: PRESSAM
INTEGER  :: SATID
INTEGER  :: TOR(5)
INTEGER  :: WINDMETH
INTEGER  :: YEAR

REAL     :: DCVALS(10000)

LOGICAL  :: ANCSECT1
LOGICAL  :: KNOTS
LOGICAL  :: ODDTOT
LOGICAL  :: OLDSECT1
LOGICAL  :: SLASHES

CHARACTER (LEN=23)    :: ENTRY
CHARACTER (LEN=10000) :: MESSAGE
CHARACTER (LEN=21)    :: SECTIONS
CHARACTER (LEN=1)     :: SECTION
CHARACTER (LEN=9)     :: SATIDSTR

! ---------------------------------------------------------------------

ENTRY=' '
SECTIONS=' 222 333 444 555 777 '

KNOTS=.FALSE.
ANCSECT1=.FALSE.  !ANCIENT SECTION ONES ARE PRE NOV 1993 CODE
OLDSECT1=.FALSE.  !    OLD SECTION ONES ARE PRE NOV 1997 CODE

MONTHDAY( 1)=31
MONTHDAY( 2)=28    ! - adjusted below
MONTHDAY( 3)=31
MONTHDAY( 4)=30
MONTHDAY( 5)=31
MONTHDAY( 6)=30
MONTHDAY( 7)=31
MONTHDAY( 8)=31
MONTHDAY( 9)=30
MONTHDAY(10)=31
MONTHDAY(11)=30
MONTHDAY(12)=31

CALL DATIM(NOW)                                ! Current date/time
IF (MOD(NOW(8),4) == 0) MONTHDAY(2)=29         ! Leap Year
IF (MOD(NOW(8),100) == 0) MONTHDAY(2)=28       ! Turn of Century
IF (MOD(NOW(8),400) == 0) MONTHDAY(2)=29       ! 400 years

IF (BULL(POINT+ 9:POINT+10) == '//') ANCSECT1=.TRUE.
IF (BULL(POINT+21:POINT+21) ==  ' ') OLDSECT1=.TRUE.

IF (ANCSECT1) THEN
   IF (NOW(6) == 1 .AND. NOW(5) < 3) THEN
      MONTH=MISSIN
   ELSE
      MONTH=NOW(7)
   END IF
ELSE
   MONTH=IVALUE(BULL(POINT+2:POINT+3))
END IF
IF (MONTH < 1 .OR. MONTH > 12) THEN
   PRINT*, 'SATOB WITH INVALID MONTH'
   RETURN
END IF

DAY=IVALUE(BULL(POINT:POINT+1))
IF (DAY > 50 .AND. DAY < 82) THEN
   DAY=DAY-50
   KNOTS=.TRUE.
END IF
IF (DAY < 1 .OR. DAY > MONTHDAY(MONTH)) THEN
   PRINT*, 'SATOB WITH INVALID DAY'
   RETURN
END IF

IF (ANCSECT1) THEN
   HOUR=IVALUE(BULL(POINT+2:POINT+3))
ELSE
   HOUR=IVALUE(BULL(POINT+6:POINT+7))
END IF
IF (HOUR < 0 .OR. HOUR > 23) THEN
   PRINT*, 'SATOB WITH INVALID HOUR'
   RETURN
END IF

IF (ANCSECT1) THEN
   MINUTE=IVALUE(BULL(POINT+4:POINT+4))
   IF (MINUTE /= MISSIN) MINUTE=MINUTE*10
ELSE
   MINUTE=IVALUE(BULL(POINT+8:POINT+9))
END IF
IF (MINUTE < 0 .OR. MINUTE > 59) MINUTE=30      !NEAR ENOUGH

DECADE=NOW(8)/10
NOWYEAR=MOD(NOW(8), 10)
IF (ANCSECT1) THEN
   YEAR=MISSIN
ELSE
   YEAR=IVALUE(BULL(POINT+4:POINT+4))
END IF
IF (YEAR == MISSIN) THEN
   IF (NOW(7) == 1 .AND. NOW(6) == 1 .AND. NOW(5) < 3) THEN
      PRINT*, 'SATOB WITH INVALID YEAR'
      RETURN
   ELSE
      YEAR=NOWYEAR    !SYSTEM YEAR OK UNLESS EARLY ON 1ST JAN
   END IF
END IF
IF (YEAR /= NOWYEAR .AND. NOWYEAR == 0) DECADE=DECADE-1
YEAR=DECADE*10 + YEAR

IF (ANCSECT1) THEN
   POINT=POINT+4
ELSE
   POINT=POINT+10
END IF

IF (ANCSECT1) THEN
   WINDMETH=MISSIN
ELSE
   WINDMETH=IVALUE(BULL(POINT:POINT))
END IF

SATID=IVALUE(BULL(POINT+2:POINT+4))
SATIDSTR(1:3)=BULL(POINT+2:POINT+4)

IF (ANCSECT1 .OR. OLDSECT1) THEN
   POINT=POINT+7
ELSE
   POINT=POINT+14  !SKIP PAST NEW F'S 6-FIGURE GROUP FOR NO
END IF             !STORAGE OF IT MAY BE INTRODUCED LATER

10 CONTINUE

IF (POINT >= BULLEND) RETURN
DO I=1, 10000
   DCVALS(I)=MISSIN
END DO
SECTION='?'
I=POINT-1
DO POINT=I, BULLEND-4
   DO J=1,17,4
      IF (BULL(POINT:POINT+4) == SECTIONS(J:J+4)) THEN
         SECTION=SECTIONS(J+1:J+1)
         GOTO 20
      END IF
   END DO
END DO
IF (SECTION == '?') THEN
!    *** PRINT*, 'NO MORE SECTIONS'
   RETURN
END IF

20 CONTINUE

IF      (SECTION == '4') THEN
   NELEM=10
!    *** DESCR(1)=51649
   DESCR(1)=IDES (309193)
ELSE IF (SECTION == '7') THEN
   NELEM=11
!    *** DESCR(1)=51650
   DESCR(1)=IDES (309194)
ELSE
   NELEM=16
!    *** DESCR(1)=51648
   DESCR(1)=IDES (309192)
END IF
IF      (SECTION == '2') THEN
   JUMP=30
ELSE IF (SECTION == '3' .OR. SECTION == '5') THEN
   JUMP=12
ELSE IF (SECTION == '4') THEN
   JUMP=6
ELSE
   JUMP=18
END IF
READ (SECTION, '(I1)') PRESSAM
!  EQUATE PRESSURE ASSESSMENT METHOD WITH SECTION NUMBER
!  UNTIL A BETTER SCHEME THOUGHT UP

POINT=POINT+5
K=POINT
NOBS=0
SLASHES=.FALSE.

30 CONTINUE

IF (SECTION == '7' .AND. BULL(K+2:K+4) == '///') K=K+6
NN=IVALUE(BULL(K+3:K+4))

IF_DCODENN: &
IF (NN < 1) THEN
!    *** PRINT*, 'UNDECODABLE NN |', BULL(K+3:K+4), '|'
   GOTO 10
ELSE
   NOBS=NOBS + NN
   IF (SECTION == '2') THEN
      IF (MOD(NN,2) == 0) THEN
         L=NN/2
         ODDTOT=.FALSE.
      ELSE
         L=NN/2 + 1
         ODDTOT=.TRUE.
         IF (.NOT.SLASHES) THEN
            M=INDEX(BULL(K:K + 6 + L*JUMP), '/////')
            IF (M > 0) SLASHES=.TRUE.
         END IF
      END IF
   ELSE IF (SECTION == '7') THEN
      IF (MOD(NN,5) == 0) THEN
         L=NN/5
      ELSE
         L=NN/5 + 1
      END IF
   ELSE
      L=NN
   END IF
   IF (SECTION == '2') THEN
      IF (.NOT.ODDTOT .OR. SLASHES) THEN
         K=K + 6 + L*JUMP
      ELSE
         K=K + 6 + (L-1)*JUMP + 18
      END IF
   ELSE
      K=K + 6 + L*JUMP
   END IF

IF_NEWSECT: &
   IF      (K > BULLEND     ) THEN
!       *** PRINT*, 'OFF END OF BULLETIN'
      NOBS=0
      RETURN
   ELSE IF (K > BULLEND-3) THEN
!       *** PRINT*, 'AT END OF BULLETIN'
      GOTO 40
   ELSE
      DO J=1,17,4
         IF (BULL(K-1:K+3) == SECTIONS(J:J+4)) THEN
!             *** PRINT*, 'NEXT SECTION FOUND'
            GOTO 40
         END IF
      END DO
      GOTO 30
   END IF IF_NEWSECT
END IF IF_DCODENN

40 CONTINUE

I=0
DO J=1,NOBS
   DCVALS(J)=SATID
END DO
IF (SECTION == '2' .OR. SECTION == '3' .OR. SECTION == '5') THEN
   I=I+NOBS
   DO J=1,NOBS
      DCVALS(I+J)=WINDMETH
   END DO
END IF
I=I+NOBS
DO J=1,NOBS
   DCVALS(I+J)=YEAR
END DO
I=I+NOBS
DO J=1,NOBS
   DCVALS(I+J)=MONTH
END DO
I=I+NOBS
DO J=1,NOBS
   DCVALS(I+J)=DAY
END DO
I=I+NOBS
DO J=1,NOBS
   DCVALS(I+J)=HOUR
END DO
I=I+NOBS
DO J=1,NOBS
   DCVALS(I+J)=MINUTE
END DO
IF (SECTION /= '7') THEN
   I=I+NOBS
   DO J=1,NOBS
      IF (SECTION == '4') THEN
         DCVALS(I+J)=0.0
      ELSE
         DCVALS(I+J)=2.0
      END IF
   END DO
END IF
IF (SECTION == '2' .OR. SECTION == '3' .OR. SECTION == '5') THEN
   I=I+NOBS
   DO J=1,NOBS
      DCVALS(I+J)=PRESSAM
   END DO
END IF
ENDDCV=I+J - 1

IF (SECTION == '2') THEN
   CALL SATOB2(POINT,KNOTS,DCVALS,NOBS,BULL,SLASHES)
ELSE IF (SECTION == '3') THEN
   CALL SATOB3(POINT,KNOTS,DCVALS,NOBS,BULL)
ELSE IF (SECTION == '4') THEN
   CALL SATOB4(POINT,DCVALS,NOBS,BULL)
ELSE IF (SECTION == '5') THEN
   CALL SATOB5(POINT,DCVALS,NOBS,BULL)
ELSE
   CALL SATOB7(POINT,DCVALS,NOBS,BULL)
END IF

ENTRY(10:10)=SECTION
CALL STBIND(NOBS,DCVALS,SATIDSTR(1:3),DATETIME,ENTRY)
CALL STBENC(DCVALS,NELEM,NOBS,DESCR,MESSAGE,MESSLEN,DATETIME)

! PUT CURRENT TIME AS TIME OF RECEIPT IN SECTION 1 OF THE BUFR MESSAGE
! (DISPLACEMENT AS FOR BUFR VERSION 1; CHANGE IF TOTAL LENGTH AT START)

TOR(1)=NOW(8)
NYEAR=MOD(NOW(8),100)
IF (NYEAR == 0) NYEAR=100
MESSAGE(17:17)=CHAR(NYEAR)

DO I=1,4
   TOR(1+I)=NOW(8-I)
   MESSAGE(17+I:17+I)=CHAR(MOD(NOW(8-I),100))
END DO
MESSAGE(13:13)=CHAR(4) ! DATA TYPE (SINGLE LEVEL, NOT SATELLITE)

!-----------------------------------------------------------------------
! Set bulletin details to go in trailer and call AIRSTO
!-----------------------------------------------------------------------

SATIDSTR=ENTRY(3:11)
ENTRY(3:6)=TTAAII(1:4)
ENTRY(7:7)=CHAR(0)
IF (OCOR) ENTRY(7:7)=CHAR(1)
ENTRY(8:11)=CCCC
BLKSIZ=27998

CALL AIRSTO(DATETIME,ENTRY,MESSAGE(:MESSLEN),  &
            NFT,BLKSIZ,SATIDSTR,TOR)

IF (BULL(POINT-1:POINT-1) /= '=') GOTO 10

RETURN
END SUBROUTINE SATOB1
